
! CRTM_Fastem4
! -----------------------------------------------------------------------------------
! This code is an extension of the FATEM3. The FASTEM 1 uses the modified
! Fresnel reflection coefficients corrected by small-scale roughness, 
! a large-scale correction, and foam for a large wind.
! The FASTEM 2 uses a modified surface reflectance based on total atmospheric
! transmittance to take account for anisotropic downward radiation in a specular-
! surface model. The FASTEM 3 added an azimuthmal components, in particular
! including the 3rd, and 4th Stokes parameters at an incident angle of 53.1 degree
! and at 4 WINDSAT frequencies.
!
! The FASTEM 1,2,and 3 is a part of the RTTOV. The model is also used for the NCEP
! radiance assimilation. The FASTEM 4 is a standalone code that can be pluged in
! any model. The permittivity model is the FASTEM 4 fits the measurements for fresh and 
! salted water and frequencies between 1.4 Ghz and 410 Ghz, thus teh FASTEM 4 is
! applicable to the ocean salinity studies. The FASTEM 4 uses Durden and Vescky
! surface roughness model adjusted with a constant factor, and the model has been
! shown approprate to the microwave sea surface emissivity calculation.
! The small-scale correction in the FASTEM 4 fits the detailed two-scale model well.
! The coefficients in the large-scale correction are generated by fitting the
! detailed two-scale model and with the new permittivity model.
! The FASTEM can also calculate the emissivity as a function of the relative azimuth
! angle, the difference between the wind direction and the sensor azimuth angle.
! Details are referred to the document by
!    
! CREATION HISTORY:
!       Written by:     
!   Quanhua (Mark) Liu, Joint Center for the Satellite Data Assimilation
!                       Quanhua.Liu@noaa.gov
!   Stephen English     Met. Office, United Kingdom
!                       Stephen.English@metoffice.gov.uk
!   Fuzhong Weng,       NOAA/NESDIS/STAR
!                       Fuzhong.Weng@noaa.gov
!   July 31, 2009
!
!       Refactored by:  Paul van Delst, October 2010
!                       paul.vandelst@noaa.gov
!
! -------------------------------------------------------------------------------------
!

MODULE CRTM_Fastem4
 
  ! -----------------
  ! Environment setup
  ! -----------------
  ! Module use
  USE Type_Kinds           , ONLY: fp
  USE Fresnel              , ONLY: fVar_type => iVar_type , &
                                   Fresnel_Reflectivity   , &
                                   Fresnel_Reflectivity_TL, &
                                   Fresnel_Reflectivity_AD
  USE Liu                  , ONLY: pVar_type => iVar_type, &
                                   Ocean_Permittivity    => Liu_Ocean_Permittivity   , &
                                   Ocean_Permittivity_TL => Liu_Ocean_Permittivity_TL, &
                                   Ocean_Permittivity_AD => Liu_Ocean_Permittivity_AD
  USE Reflection_Correction, ONLY: rcVar_type => iVar_type, &
                                   Compute_R_Correction   , &
                                   Compute_R_Correction_TL, &
                                   Compute_R_Correction_AD
  USE Azimuth_Emissivity   , ONLY: aeVar_type => iVar_type, &
                                   Compute_Azimuth_Emissivity   , &
                                   Compute_Azimuth_Emissivity_TL, &
                                   Compute_Azimuth_Emissivity_AD
  USE CRTM_Parameters      , ONLY: PI, DEGREES_TO_RADIANS, &
                                   ZERO, ONE, TWO, THREE, POINT_5
  ! Disable implicit typing
  IMPLICIT NONE


  ! ------------
  ! Visibilities
  ! ------------
  PRIVATE
  ! Data types
  PUBLIC :: iVar_type
  ! Science routines
  PUBLIC :: Compute_FASTEM4
  PUBLIC :: Compute_FASTEM4_TL
  PUBLIC :: Compute_FASTEM4_AD


  ! -----------------
  ! Module parameters
  ! -----------------
  CHARACTER(*), PARAMETER :: MODULE_VERSION_ID = &
  '$Id$'

  ! Stokes component information
  ! ...The number of Stokes components
  INTEGER, PARAMETER :: N_STOKES = 4
  ! ...The vector indices
  INTEGER, PARAMETER :: Iv_IDX = 1 ! Describes vertical polarization
  INTEGER, PARAMETER :: Ih_IDX = 2 ! Describes horizontal polarization
  INTEGER, PARAMETER :: U_IDX  = 3 ! Describes plane of polarization
  INTEGER, PARAMETER :: V_IDX  = 4 ! Describes ellipticity of polarization
  
  ! Invalid indicators
  REAL(fp), PARAMETER :: INVALID_AZIMUTH_ANGLE = -999.0_fp  
  REAL(fp), PARAMETER :: INVALID_TRANSMITTANCE = -999.0_fp  ! Disable non-specular correction

  ! Foam coverage
  ! ...Wind speed threshold (m/s) for foam
  REAL(fp), PARAMETER :: FOAM_THRESHOLD = 0.0_fp
  ! ...Foam coverage coefficients. See:
  ! See Eqn.(1) in 
  !   Liu, Q. et al. (1998) Monte Carlo simulations of the
  !     microwave emissivity of the sea surface.
  !     JGR, Volume 103, No.C11, Pages 24983-24989
  REAL(fp), PARAMETER :: FC1 = 7.75e-06_fp ! FWD model
  REAL(fp), PARAMETER :: FC2 = 3.231_fp     ! FWD model
!  REAL(fp), PARAMETER :: FC3 = FC1*FC2      ! TL model
!  REAL(fp), PARAMETER :: FC4 = FC2-ONE      ! TL model
  !   English, S., and T. Hewison, (1998) A fast generic
  !     millimeter-wave emissivity model,
  !     Proceedings of SPIE, 3503, pp288-300
!  REAL(fp), PARAMETER :: FC1 = 1.95E-05_fp  ! FWD model
!  REAL(fp), PARAMETER :: FC2 = 2.55_fp      ! FWD model
  REAL(fp), PARAMETER :: FC3 = FC1*FC2      ! TL model
  REAL(fp), PARAMETER :: FC4 = FC2-ONE      ! TL model

  ! minimum and maximum frequency
  REAL( fp ), PUBLIC, PARAMETER ::  min_f = 1.4_fp
  REAL( fp ), PUBLIC, PARAMETER ::  max_f = 200.0_fp
  ! minimum and maximum wind speed
  REAL( fp ), PUBLIC, PARAMETER ::  min_wind = 0.3_fp
  REAL( fp ), PUBLIC, PARAMETER ::  max_wind = 35.0_fp
  
  ! Fitting coefficients
!  ! ...for the small and large-scale correction, Nov. 15,2009 azimuth
!  REAL(fp), PARAMETER :: Lcoef(36) = (/ & 
!    -2.748114E-02_fp, 4.492694E-04_fp,-9.032870E-07_fp, 3.634301E-02_fp,-5.522355E-04_fp, &
!     1.047926E-06_fp,-1.106594E-02_fp, 1.491033E-04_fp,-2.683134E-07_fp, 1.896311E-03_fp, &
!     1.501236E-05_fp,-7.632568E-08_fp, 1.166231E-05_fp, 2.759462E-07_fp,-1.615886E-09_fp, &
!    -1.094748E-03_fp,-2.604274E-05_fp, 1.115938E-07_fp,-2.556949E-02_fp,-6.744277E-05_fp, &
!     9.892297E-07_fp, 2.998525E-02_fp, 1.677300E-04_fp,-1.551966E-06_fp,-6.619821E-03_fp, &
!    -5.414996E-05_fp, 4.390623E-07_fp, 7.539377E-04_fp,-3.393878E-05_fp, 1.007074E-07_fp, &
!     1.166231E-05_fp, 2.759462E-07_fp,-1.615886E-09_fp, 4.762508E-05_fp, 2.290840E-05_fp, &
!    -6.543931E-08_fp /)  

  ! fitting coefficients for the large-scale correction   select 1
  REAL(fp),  PUBLIC, PARAMETER :: Lcoef(36) = (/ &
  -9.197134E-02_fp, 8.310678E-04_fp,-6.065411E-07_fp, 1.350073E-01_fp,-1.032096E-03_fp, &
   4.259935E-07_fp,-4.373322E-02_fp, 2.545863E-04_fp, 9.835554E-08_fp,-1.199751E-03_fp, &
   1.360423E-05_fp,-2.088404E-08_fp,-2.201640E-05_fp, 1.951581E-07_fp,-2.599185E-10_fp, &
   4.477322E-04_fp,-2.986217E-05_fp, 9.406466E-08_fp,-7.103127E-02_fp,-4.713113E-05_fp, &
   1.754742E-06_fp, 9.720859E-02_fp, 1.374668E-04_fp,-2.591771E-06_fp,-2.687455E-02_fp, &
  -3.677779E-05_fp, 7.548377E-07_fp,-3.049506E-03_fp,-5.412826E-05_fp, 2.285387E-07_fp, &
  -2.201640E-05_fp, 1.951581E-07_fp,-2.599185E-10_fp, 2.297488E-03_fp, 3.787032E-05_fp, &
  -1.553581E-07_fp /) 

  ! fitting coefficients for the small-scale correction
  REAL(fp),  PUBLIC, PARAMETER :: Scoef(8) = (/ &   
    -5.0208480E-06_fp,   2.3297951E-08_fp,   4.6625726E-08_fp,  -1.9765665E-09_fp, &
    -7.0469823E-04_fp,   7.5061193E-04_fp,   9.8103876E-04_fp,   1.5489504E-04_fp /)

  ! --------------------------------------
  ! Structure definition to hold internal
  ! variables across FWD, TL, and AD calls
  ! --------------------------------------
  TYPE :: iVar_type
    PRIVATE
    ! Validity indicator
    LOGICAL :: Is_Valid = .FALSE.
    ! Forward model input values
    REAL(fp) :: Frequency    = ZERO
    REAL(fp) :: Zenith_Angle = ZERO
    REAL(fp) :: Temperature  = ZERO
    REAL(fp) :: Salinity     = ZERO
    REAL(fp) :: Wind_Speed   = ZERO
    ! ...Optional
    LOGICAL  :: Azimuth_Angle_Valid = .FALSE.
    REAL(fp) :: Azimuth_Angle       = ZERO
    LOGICAL  :: Transmittance_Valid = .FALSE.
    REAL(fp) :: Transmittance       = ZERO
    ! The zenith angle term
    REAL(fp) :: cos_z = ONE
    ! The permittivity term
    COMPLEX(fp) :: Permittivity = ZERO
    ! The Fresnel reflectivity terms
    REAL(fp) :: Rv_Fresnel = ZERO
    REAL(fp) :: Rh_Fresnel = ZERO
    ! Foam Terms
    REAL(fp) :: Rv_Foam = ZERO
    REAL(fp) :: Rh_Foam = ZERO
    REAL(fp) :: Foam_Cover = ZERO
    ! Large scale correction reflectivities
    REAL(fp) :: Rv_Large = ZERO
    REAL(fp) :: Rh_Large = ZERO
    
    ! Small scale correction reflectivities
    REAL(fp) :: scor = ZERO
    REAL(fp) :: small_corr = ONE
    REAL(fp) :: freq_S
    REAL(fp) :: windspeed

    ! Final reflectivities
    REAL(fp) :: Rv = ZERO
    REAL(fp) :: Rh = ZERO
    ! Azimuthal emissivity
    REAL(fp) :: e_Azimuth(N_STOKES) = ZERO
    ! Anisotropic downward radiation correction
    REAL(fp) :: Rv_Mod = ZERO
    REAL(fp) :: Rh_Mod = ZERO
    ! The final emissivity
    REAL(fp) :: e(N_STOKES) = ZERO
    ! Internal variables for subcomponents
    TYPE(pVar_type)  :: pVar
    TYPE(fVar_type)  :: fVar
    TYPE(aeVar_type) :: aeVar
    TYPE(rcVar_type) :: rcVar
  END TYPE iVar_type


CONTAINS


!################################################################################
!################################################################################
!##                                                                            ##
!##                         ## PUBLIC MODULE ROUTINES ##                       ##
!##                                                                            ##
!################################################################################
!################################################################################

!--------------------------------------------------------------------------------
!:sdoc+:
!
! NAME:
!       Compute_FASTEM4
!
! PURPOSE:
!       Subroutine to compute the FASTEM4 microwave sea surface emissivity
!       and reflectivity. 
!
! CALLING SEQUENCE:
!       CALL Compute_FASTEM4( &
!              Frequency    , &  ! Input
!              Zenith_Angle , &  ! Input
!              Temperature  , &  ! Input
!              Salinity     , &  ! Input
!              Wind_Speed   , &  ! Input
!              iVar         , &  ! Internal variable output
!              Emissivity   , &  ! Output
!              Reflectivity , &  ! Output
!              Azimuth_Angle, &  ! Optional input
!              Transmittance  )  ! Optional input
!                             
!
! INPUTS:
!       Frequency:      Microwave frequency.
!                       UNITS:      GHz
!                       TYPE:       REAL(fp)
!                       DIMENSION:  Scalar
!                       ATTRIBUTES: INTENT(IN)
!
!       Zenith_Angle:   Sensor zenith angle at the sea surface
!                       UNITS:      Degrees
!                       TYPE:       REAL(fp)
!                       DIMENSION:  Scalar
!                       ATTRIBUTES: INTENT(IN)
!
!       Temperature:    Sea surface temperature
!                       UNITS:      Kelvin, K
!                       TYPE:       REAL(fp)
!                       DIMENSION:  Scalar
!                       ATTRIBUTES: INTENT(IN)
!
!       Salinity:       Water salinity
!                       UNITS:      ppt (parts per thousand)
!                       TYPE:       REAL(fp)
!                       DIMENSION:  Scalar
!                       ATTRIBUTES: INTENT(IN)
!
!       Wind_Speed:     Sea surface wind speed
!                       UNITS:      m/s
!                       TYPE:       REAL(fp)
!                       DIMENSION:  Scalar
!                       ATTRIBUTES: INTENT(IN)
!
! OUTPUTS:
!       iVar:           Structure containing internal variables required for
!                       subsequent tangent-linear or adjoint model calls.
!                       The contents of this structure are NOT accessible
!                       outside of this module.
!                       UNITS:      N/A
!                       TYPE:       iVar_type
!                       DIMENSION:  Scalar
!                       ATTRIBUTES: INTENT(OUT)
!
!       Emissivity:     The surface emissivity 
!                       UNITS:      N/A
!                       TYPE:       REAL(fp)
!                       DIMENSION:  Rank-1, 4-elements (n_Stokes)
!                       ATTRIBUTES: INTENT(OUT)
!
!       Reflectivity:   The surface reflectivity.
!                       UNITS:      N/A
!                       TYPE:       REAL(fp)
!                       DIMENSION:  Rank-1, 4-elements (n_Stokes)
!                       ATTRIBUTES: INTENT(OUT)
!
! OPTIONAL INPUTS:
!       Azimuth_Angle:  Relative azimuth angle (wind direction - sensor azimuth)
!                       UNITS:      Degrees
!                       TYPE:       REAL(fp)
!                       DIMENSION:  Scalar
!                       ATTRIBUTES: INTENT(IN), OPTIONAL
!
!       Transmittance:  Total atmospheric transmittance 
!                       UNITS:      N/A
!                       TYPE:       REAL(fp)
!                       DIMENSION:  Scalar
!                       ATTRIBUTES: INTENT(IN), OPTIONAL
!
!:sdoc-:
!--------------------------------------------------------------------------------

  SUBROUTINE Compute_FASTEM4( &
    Frequency    , &  ! Input
    Zenith_Angle , &  ! Input
    Temperature  , &  ! Input
    Salinity     , &  ! Input
    Wind_Speed   , &  ! Input
    iVar         , &  ! Internal variable output
    Emissivity   , &  ! Output
    Reflectivity , &  ! Output
    Azimuth_Angle, &  ! Optional Input
    Transmittance  )  ! Optional Input
    ! Arguments
    REAL(fp),           INTENT(IN)  :: Frequency
    REAL(fp),           INTENT(IN)  :: Zenith_Angle
    REAL(fp),           INTENT(IN)  :: Temperature 
    REAL(fp),           INTENT(IN)  :: Salinity
    REAL(fp),           INTENT(IN)  :: Wind_Speed
    TYPE(iVar_type),    INTENT(OUT) :: iVar
    REAL(fp),           INTENT(OUT) :: Emissivity(:)
    REAL(fp),           INTENT(OUT) :: Reflectivity(:)
    REAL(fp), OPTIONAL, INTENT(IN)  :: Azimuth_Angle
    REAL(fp), OPTIONAL, INTENT(IN)  :: Transmittance

    ! Setup
    ! ...Save forward input variables for TL and AD calculations
    iVar%Frequency    = Frequency   
    iVar%Zenith_Angle = Zenith_Angle
    iVar%Temperature  = Temperature 
    iVar%Salinity     = Salinity    
    iVar%Wind_Speed   = Wind_Speed
    ! ...Save derived variables
    iVar%cos_z = COS(Zenith_Angle*DEGREES_TO_RADIANS)
    
    ! Permittivity calculation
    CALL Ocean_Permittivity( Temperature, Salinity, Frequency, &
                             iVar%Permittivity, &
                             iVar%pVar )


    ! Fresnel reflectivity calculation
    CALL Fresnel_Reflectivity( iVar%Permittivity, iVar%cos_z, &
                               iVar%Rv_Fresnel, iVar%Rh_Fresnel, &
                               iVar%fVar )


    ! Foam reflectivity calculation
    CALL Foam_Reflectivity( Zenith_Angle, Frequency, &
                            iVar%Rv_Foam, iVar%Rh_Foam )


    ! Foam coverage calculation
    CALL Foam_Coverage( Wind_Speed, &
                        iVar%Foam_Cover )
    
    
    ! Large scale correction calculation
    CALL Large_Scale_Correction( Wind_Speed, iVar%cos_z, Frequency, &
                                 iVar%Rv_Large, iVar%Rh_Large )
    
    ! Small scale correction calculation, Var%small_corr
    CALL Small_Scale_Correction( Wind_Speed, Frequency, iVar )

    ! Compute the first two Stokes components of the emissivity
    iVar%Rv = iVar%Rv_Fresnel*iVar%small_corr -iVar%Rv_Large
    iVar%Rh = iVar%Rh_Fresnel*iVar%small_corr -iVar%Rh_Large
    Emissivity(Iv_IDX) = ONE - (ONE-iVar%Foam_Cover)*iVar%Rv - iVar%Foam_Cover*iVar%Rv_Foam
    Emissivity(Ih_IDX) = ONE - (ONE-iVar%Foam_Cover)*iVar%Rh - iVar%Foam_Cover*iVar%Rh_Foam
    
    ! Azimuthal component calculation
    iVar%Azimuth_Angle_Valid = .FALSE.
    iVar%e_Azimuth = ZERO
    IF ( PRESENT(Azimuth_Angle) ) THEN
      IF ( ABS(Azimuth_Angle) <= 360.0_fp ) THEN
        CALL Compute_Azimuth_Emissivity( &
               Wind_Speed, Azimuth_Angle, Frequency, iVar%cos_z, &
               iVar%aeVar, &
               iVar%e_Azimuth)
        iVar%Azimuth_Angle_Valid = .TRUE.
        iVar%Azimuth_Angle       = Azimuth_Angle
      END IF
    END IF

    ! Anisotropic downward radiation correction calculation
    iVar%Transmittance_Valid = .FALSE.
    iVar%Rv_Mod = ONE
    iVar%Rh_Mod = ONE  
    IF ( PRESENT(Transmittance) ) THEN
      IF ( Transmittance > ZERO .AND. Transmittance < ONE ) THEN
        CALL Compute_R_Correction( &
               Transmittance, Wind_Speed, iVar%cos_z, Frequency, &
               iVar%rcVar, &
               iVar%Rv_Mod, iVar%Rh_Mod )
        iVar%Transmittance_Valid = .TRUE.
        iVar%Transmittance       = Transmittance
      END IF
    END IF 
 
 
    ! Assemble the return...
    ! ...emissivities
    Emissivity(Iv_IDX) = Emissivity(Iv_IDX) + iVar%e_Azimuth(Iv_IDX)
    Emissivity(Ih_IDX) = Emissivity(Ih_IDX) + iVar%e_Azimuth(Ih_IDX)  
    Emissivity(U_IDX)  = iVar%e_Azimuth(U_IDX)
    Emissivity(V_IDX)  = iVar%e_Azimuth(V_IDX)
    ! ...reflectivties
    Reflectivity(Iv_IDX)      = iVar%Rv_Mod * (ONE-Emissivity(Iv_IDX))
    Reflectivity(Ih_IDX)      = iVar%Rh_Mod * (ONE-Emissivity(Ih_IDX))  
    Reflectivity(U_IDX:V_IDX) = ZERO   ! 3rd, 4th Stokes from atmosphere are not included.
    ! ...save the emissivity for TL and AD reflectivity calculations
    iVar%e = Emissivity

    
    ! Flag the internal variable structure as valid
    iVar%Is_Valid = .TRUE.
    
  END SUBROUTINE Compute_FASTEM4


!--------------------------------------------------------------------------------
!:sdoc+:
!
! NAME:
!       Compute_FASTEM4_TL
!
! PURPOSE:
!       Subroutine to compute the tangent-linear FASTEM4 microwave sea
!       surface emissivity and reflectivity.
!
!       NOTE: The forward model must be called first to fill the internal
!             variable argument with the intermediate forward calculations.
!
! CALLING SEQUENCE:
!       CALL Compute_FASTEM4_TL( &
!         Temperature_TL  , &  ! TL  Input
!         Salinity_TL     , &  ! TL  Input
!         Wind_Speed_TL   , &  ! TL  Input
!         iVar            , &  ! Internal variable input
!         Emissivity_TL   , &  ! TL  Output
!         Reflectivity_TL , &  ! TL  Output
!         Azimuth_Angle_TL, &  ! Optional TL  input
!         Transmittance_TL  )  ! Optional TL  input
!                             
!
! INPUTS:
!       Temperature_TL:    Tangent-linear sea surface temperature
!                          UNITS:      Kelvin, K
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Scalar
!                          ATTRIBUTES: INTENT(IN)
!
!       Salinity_TL:       Tangent-linear water salinity
!                          UNITS:      ppt (parts per thousand)
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Scalar
!                          ATTRIBUTES: INTENT(IN)
!
!       Wind_Speed_TL:     Tangent-linear sea surface wind speed
!                          UNITS:      m/s
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Scalar
!                          ATTRIBUTES: INTENT(IN)
!
!       iVar:              Structure containing internal variables computed
!                          in a previous forward model call.
!                          The contents of this structure are NOT accessible
!                          outside of this module.
!                          UNITS:      N/A
!                          TYPE:       iVar_type
!                          DIMENSION:  Scalar
!                          ATTRIBUTES: INTENT(IN)
!
! OUTPUTS:
!       Emissivity_TL:     Tangent-linear surface emissivity 
!                          UNITS:      N/A
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Rank-1, 4-elements (n_Stokes)
!                          ATTRIBUTES: INTENT(OUT)
!
!       Reflectivity_TL:   Tangent-linear surface reflectivity.
!                          UNITS:      N/A
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Rank-1, 4-elements (n_Stokes)
!                          ATTRIBUTES: INTENT(OUT)
!
! OPTIONAL INPUTS:
!       Azimuth_Angle_TL:  Tangent-linear relative azimuth angle.
!                          This argument is ignored if a valid relative azimuth
!                          angle was not passed into the forward model call.
!                          UNITS:      Degrees
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Scalar
!                          ATTRIBUTES: INTENT(IN), OPTIONAL
!
!       Transmittance_TL:  Tangent-linear total atmospheric transmittance
!                          This argument is ignored if a valid transmittance
!                          was not passed into the forward model call.
!                          UNITS:      N/A
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Scalar
!                          ATTRIBUTES: INTENT(IN), OPTIONAL
!
!:sdoc-:
!--------------------------------------------------------------------------------

  SUBROUTINE Compute_FASTEM4_TL( &
    Temperature_TL  , &  ! TL  Input
    Salinity_TL     , &  ! TL  Input
    Wind_Speed_TL   , &  ! TL  Input
    iVar            , &  ! Internal variable input
    Emissivity_TL   , &  ! TL Output
    Reflectivity_TL , &  ! TL Output
    Azimuth_Angle_TL, &  ! Optional TL  input
    Transmittance_TL  )  ! Optional TL  input
    ! Arguments
    REAL(fp),           INTENT(IN)  :: Temperature_TL    
    REAL(fp),           INTENT(IN)  :: Salinity_TL
    REAL(fp),           INTENT(IN)  :: Wind_Speed_TL
    TYPE(iVar_type),    INTENT(IN)  :: iVar
    REAL(fp),           INTENT(OUT) :: Emissivity_TL(:)
    REAL(fp),           INTENT(OUT) :: Reflectivity_TL(:)
    REAL(fp), OPTIONAL, INTENT(IN)  :: Azimuth_Angle_TL
    REAL(fp), OPTIONAL, INTENT(IN)  :: Transmittance_TL
    ! Local variables
    REAL(fp) :: Rv_Fresnel_TL, Rh_Fresnel_TL
    REAL(fp) :: Rv_Foam_TL   , Rh_Foam_TL
    REAL(fp) :: Rv_Large_TL  , Rh_Large_TL
    REAL(fp) :: Rv_TL        , Rh_TL
    REAL(fp) :: Rv_Mod_TL    , Rh_Mod_TL
    REAL(fp) :: Foam_Cover_TL
    REAL(fp) :: e_Azimuth_TL(N_STOKES)
    COMPLEX(fp) :: Permittivity_TL
    REAL(fp) :: small_corr_TL

    ! Check internal structure
    IF ( .NOT. iVar%Is_Valid ) THEN
      Emissivity_TL   = ZERO
      Reflectivity_TL = ZERO
      RETURN
    END IF
    
    
    ! Permittivity calculation
    CALL Ocean_Permittivity_TL( Temperature_TL, Salinity_TL, iVar%Frequency, &
                                Permittivity_TL, &
                                iVar%pVar)


    ! Fresnel reflectivity calculation
    CALL Fresnel_Reflectivity_TL( permittivity_TL, iVar%cos_z, &
                                  Rv_Fresnel_TL, Rh_Fresnel_TL, &
                                  iVar%fVar )


    ! Foam reflectivity "calculation"
    Rv_Foam_TL = ZERO
    Rh_Foam_TL = ZERO


    ! Foam coverage calculation
    CALL Foam_Coverage_TL( iVar%Wind_Speed, Wind_Speed_TL, &
                           Foam_Cover_TL )


    ! Large Scale Correction Calculation
    CALL Large_Scale_Correction_TL( iVar%Wind_Speed, iVar%cos_z, iVar%Frequency, Wind_Speed_TL, &
                                    Rv_Large_TL, Rh_Large_TL )    

    ! Small Scale Correction Calculation 
    CALL Small_Scale_Correction_TL( Wind_Speed_TL, iVar, small_corr_TL )  
      
    ! Compute the first two Stokes components of the tangent-linear emissivity
    Rv_TL = Rv_Fresnel_TL*iVar%small_corr + iVar%Rv_Fresnel*small_corr_TL - Rv_Large_TL
    Emissivity_TL(Iv_IDX) = (iVar%Foam_Cover-ONE)*Rv_TL + &
                            (iVar%Rv-iVar%Rv_Foam)*Foam_Cover_TL - &
                            iVar%Foam_Cover*Rv_Foam_TL
    Rh_TL = Rh_Fresnel_TL*iVar%small_corr + iVar%Rh_Fresnel*small_corr_TL - Rh_Large_TL
    Emissivity_TL(Ih_IDX) = (iVar%Foam_Cover-ONE)*Rh_TL + &
                            (iVar%Rh-iVar%Rh_Foam)*Foam_Cover_TL - &
                            iVar%Foam_Cover*Rh_Foam_TL
     
    ! Azimuthal component calculation
    IF ( PRESENT(Azimuth_Angle_TL) .AND. iVar%Azimuth_Angle_Valid ) THEN
      CALL Compute_Azimuth_Emissivity_TL( &
             Wind_Speed_TL, Azimuth_Angle_TL, &
             iVar%aeVar, &
             e_Azimuth_TL )
    ELSE
      e_Azimuth_TL = ZERO
    END IF


    ! Anisotropic downward radiation correction calculation
    IF ( PRESENT(Transmittance_TL) .AND. iVar%Transmittance_Valid ) THEN
      CALL Compute_R_Correction_TL( &
             Transmittance_TL, Wind_Speed_TL, &
             iVar%rcVar, &
             Rv_Mod_TL,Rh_Mod_TL)
    ELSE
      Rv_Mod_TL = ZERO
      Rh_Mod_TL = ZERO
    END IF
    

    ! Compute the tangent-linear...
    ! ...emissivities
    Emissivity_TL(Iv_IDX) = Emissivity_TL(Iv_IDX) + e_Azimuth_TL(Iv_IDX)
    Emissivity_TL(Ih_IDX) = Emissivity_TL(Ih_IDX) + e_Azimuth_TL(Ih_IDX)
    Emissivity_TL(U_IDX)  = e_Azimuth_TL(U_IDX)
    Emissivity_TL(V_IDX)  = e_Azimuth_TL(V_IDX)
    ! ...reflectivities
    Reflectivity_TL(Iv_IDX)      = (ONE-iVar%e(Iv_IDX))*Rv_Mod_TL - iVar%Rv_Mod*Emissivity_TL(Iv_IDX)
    Reflectivity_TL(Ih_IDX)      = (ONE-iVar%e(Ih_IDX))*Rh_Mod_TL - iVar%Rh_Mod*Emissivity_TL(Ih_IDX)
    Reflectivity_TL(U_IDX:V_IDX) = ZERO  ! 3rd, 4th Stokes from atmosphere are not included.
    
  END SUBROUTINE Compute_FASTEM4_TL


!--------------------------------------------------------------------------------
!:sdoc+:
!
! NAME:
!       Compute_FASTEM4_AD
!
! PURPOSE:
!       Subroutine to compute the adjoint FASTEM4 microwave sea
!       surface emissivity and reflectivity.
!
!       NOTE: The forward model must be called first to fill the internal
!             variable argument with the intermediate forward calculations.
!
! CALLING SEQUENCE:
!       CALL Compute_FASTEM4_AD( &
!         Emissivity_AD   , &  ! AD  Input
!         Reflectivity_AD , &  ! AD  Input
!         iVar            , &  ! Internal variable input
!         Temperature_AD  , &  ! AD  Output
!         Salinity_AD     , &  ! AD  Output
!         Wind_Speed_AD   , &  ! AD  Output
!         Azimuth_Angle_AD, &  ! Optional AD Output
!         Transmittance_AD  )  ! Optional AD Output
!                             
!
! INPUTS:
!       Emissivity_AD:     Adjoint surface emissivity
!                          ***SET TO ZERO UPON EXIT***
!                          UNITS:      N/A
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Rank-1, 4-elements (n_Stokes)
!                          ATTRIBUTES: INTENT(IN OUT)
!
!       Reflectivity_AD:   Adjoint surface reflectivity.
!                          ***SET TO ZERO UPON EXIT***
!                          UNITS:      N/A
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Rank-1, 4-elements (n_Stokes)
!                          ATTRIBUTES: INTENT(IN OUT)
!
!       iVar:              Structure containing internal variables computed
!                          in a previous forward model call.
!                          The contents of this structure are NOT accessible
!                          outside of this module.
!                          UNITS:      N/A
!                          TYPE:       iVar_type
!                          DIMENSION:  Scalar
!                          ATTRIBUTES: INTENT(IN)
!
! OUTPUTS:
!       Temperature_AD:    Adjoint sea surface temperature
!                          ***MUST CONTAIN VALUE UPON ENTRY***
!                          UNITS:      K^-1
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Scalar
!                          ATTRIBUTES: INTENT(IN OUT)
!
!       Salinity_AD:       Adjoint water salinity
!                          ***MUST CONTAIN VALUE UPON ENTRY***
!                          UNITS:      ppt^-1 (parts per thousand)
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Scalar
!                          ATTRIBUTES: INTENT(IN OUT)
!
!       Wind_Speed_AD:     Adjoint sea surface wind speed
!                          ***MUST CONTAIN VALUE UPON ENTRY***
!                          UNITS:      (m/s)^-1
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Scalar
!                          ATTRIBUTES: INTENT(IN)
!
! OPTIONAL OUTPUTS:
!       Azimuth_Angle_AD:  Adjoint relative azimuth angle.
!                          This argument is ignored if a valid relative azimuth
!                          angle was not passed into the forward model call.
!                          UNITS:      Degrees^-1
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Scalar
!                          ATTRIBUTES: INTENT(IN), OPTIONAL
!
!       Transmittance_AD:  Adjoint total atmospheric transmittance
!                          This argument is ignored if a valid transmittance
!                          was not passed into the forward model call.
!                          UNITS:      N/A
!                          TYPE:       REAL(fp)
!                          DIMENSION:  Scalar
!                          ATTRIBUTES: INTENT(IN), OPTIONAL
!
!:sdoc-:
!--------------------------------------------------------------------------------

  SUBROUTINE Compute_FASTEM4_AD( &
    Emissivity_AD   , &  ! AD Input
    Reflectivity_AD , &  ! AD Input
    iVar            , &  ! Internal variable input
    Temperature_AD  , &  ! AD Output
    Salinity_AD     , &  ! AD Output
    Wind_Speed_AD   , &  ! AD Output
    Azimuth_Angle_AD, &  ! Optional AD Output
    Transmittance_AD )   ! Optional AD Output
    ! Arguments
    REAL(fp),           INTENT(IN OUT) :: Emissivity_AD(:)
    REAL(fp),           INTENT(IN OUT) :: Reflectivity_AD(:)
    TYPE(iVar_type),    INTENT(IN)     :: iVar
    REAL(fp),           INTENT(IN OUT) :: Temperature_AD
    REAL(fp),           INTENT(IN OUT) :: Salinity_AD
    REAL(fp),           INTENT(IN OUT) :: Wind_Speed_AD
    REAL(fp), OPTIONAL, INTENT(IN OUT) :: Azimuth_Angle_AD
    REAL(fp), OPTIONAL, INTENT(IN OUT) :: Transmittance_AD
    ! Local variables
    REAL(fp) :: Rv_Fresnel_AD, Rh_Fresnel_AD
    REAL(fp) :: Rv_Foam_AD   , Rh_Foam_AD
    REAL(fp) :: Rv_Large_AD  , Rh_Large_AD
    REAL(fp) :: Rv_AD        , Rh_AD
    REAL(fp) :: Rv_Mod_AD    , Rh_Mod_AD
    REAL(fp) :: Foam_Cover_AD
    REAL(fp) :: e_Azimuth_AD(N_STOKES)
    COMPLEX(fp) :: Permittivity_AD
    REAL(fp) :: small_corr_AD

    ! Check internal structure
    IF ( .NOT. iVar%Is_Valid ) THEN
      Temperature_AD = ZERO
      Salinity_AD    = ZERO
      Wind_Speed_AD  = ZERO
      IF ( PRESENT(Azimuth_Angle_AD) ) Azimuth_Angle_AD = ZERO
      IF ( PRESENT(Transmittance_AD) ) Transmittance_AD = ZERO
      RETURN
    END IF
    

    ! Compute the adjoint of the...
    ! ...reflectivities
    Reflectivity_AD(U_IDX:V_IDX) = ZERO  ! 3rd, 4th Stokes from atmosphere are not included.

    Emissivity_AD(Ih_IDX)   = Emissivity_AD(Ih_IDX) - iVar%Rh_Mod*Reflectivity_AD(Ih_IDX)
    Rh_Mod_AD               = (ONE-iVar%e(Ih_IDX))*Reflectivity_AD(Ih_IDX)
    Reflectivity_AD(Ih_IDX) = ZERO

    Emissivity_AD(Iv_IDX)   = Emissivity_AD(Iv_IDX) - iVar%Rv_Mod*Reflectivity_AD(Iv_IDX)
    Rv_Mod_AD               = (ONE-iVar%e(Iv_IDX))*Reflectivity_AD(Iv_IDX)
    Reflectivity_AD(Iv_IDX) = ZERO
    
    ! ...emissivities    
    e_Azimuth_AD(V_IDX)  = Emissivity_AD(V_IDX); Emissivity_AD(V_IDX) = ZERO
    e_Azimuth_AD(U_IDX)  = Emissivity_AD(U_IDX); Emissivity_AD(U_IDX) = ZERO
    e_Azimuth_AD(Ih_IDX) = Emissivity_AD(Ih_IDX) ! No zero of Emissivity_AD(Ih_IDX)
    e_Azimuth_AD(Iv_IDX) = Emissivity_AD(Iv_IDX) ! No zero of Emissivity_AD(Ih_IDX)
     

    ! Anisotropic downward radiation correction calculation
    IF ( PRESENT(Transmittance_AD) .AND. iVar%Transmittance_Valid ) THEN
       CALL Compute_R_Correction_AD( &
              Rv_Mod_AD, Rh_Mod_AD, &
              iVar%rcVar, &
              Transmittance_AD, Wind_Speed_AD )
    ELSE
      Rv_Mod_AD = ZERO
      Rh_Mod_AD = ZERO
    END IF


    ! Azimuthal component calculation
    IF ( PRESENT(Azimuth_Angle_AD) .AND. iVar%Azimuth_Angle_Valid ) THEN
      CALL Compute_Azimuth_Emissivity_AD( &
             e_Azimuth_AD, &
             iVar%aeVar, &
             Wind_Speed_AD, Azimuth_Angle_AD )
    ELSE
      e_Azimuth_AD = ZERO
    END IF

    ! Compute the adjoint of the first two Stokes components of the emissivity
    Rh_Foam_AD    = -iVar%Foam_Cover      *Emissivity_AD(Ih_IDX)
    Foam_Cover_AD = (iVar%Rh-iVar%Rh_Foam)*Emissivity_AD(Ih_IDX)
    Rh_AD         = (iVar%Foam_Cover-ONE) *Emissivity_AD(Ih_IDX)
    Emissivity_AD(Ih_IDX) = ZERO
    Rh_Large_AD   = -Rh_AD
    small_corr_AD = iVar%Rh_Fresnel*Rh_AD
    Rh_Fresnel_AD =  Rh_AD*iVar%small_corr
    Rh_AD = ZERO

    Rv_Foam_AD    = -iVar%Foam_Cover      *Emissivity_AD(Iv_IDX)
    Foam_Cover_AD = (iVar%Rv-iVar%Rv_Foam)*Emissivity_AD(Iv_IDX) + Foam_Cover_AD
    Rv_AD         = (iVar%Foam_Cover-ONE) *Emissivity_AD(Iv_IDX)
    Emissivity_AD(Iv_IDX) = ZERO
    Rv_Large_AD   = -Rv_AD
    small_corr_AD = small_corr_AD + iVar%Rv_Fresnel*Rv_AD    
    Rv_Fresnel_AD =  Rv_AD*iVar%small_corr
    Rv_AD = ZERO


    ! Small scale Correction Calculation AD
    CALL Small_Scale_Correction_AD( small_corr_AD, iVar, Wind_Speed_AD )

    
    ! Large Scale Correction Calculation
    CALL Large_Scale_Correction_AD( iVar%Wind_Speed, iVar%cos_z, iVar%Frequency, &
                                    Rv_Large_AD, Rh_Large_AD, &
                                    Wind_Speed_AD )


 
    ! Foam coverage calculation
    CALL Foam_Coverage_AD( iVar%Wind_Speed, &
                           Foam_Cover_AD, &
                           Wind_Speed_AD )

                         
    ! Foam reflectivity "calculation"
    Rv_Foam_AD = ZERO
    Rh_Foam_AD = ZERO
    
    
    ! Fresnel reflectivity calculation
    Permittivity_AD = ZERO
    CALL Fresnel_Reflectivity_AD( Rv_Fresnel_AD, Rh_Fresnel_AD, iVar%cos_z, &
                                  Permittivity_AD, &
                                  iVar%fVar )
 
    ! Permittivity calculation
    CALL Ocean_Permittivity_AD( Permittivity_AD, iVar%Frequency, &
                                Temperature_AD, Salinity_AD, &
                                iVar%pVar )

  END SUBROUTINE Compute_FASTEM4_AD


!################################################################################
!################################################################################
!##                                                                            ##
!##                        ## PRIVATE MODULE ROUTINES ##                       ##
!##                                                                            ##
!################################################################################
!################################################################################

  ! ===================================================================
  ! Foam coverage routines
  ! Whitecaps and the passive remote sensing of the ocean surface
  !   Monahan, E.C., and O'Muircheartaigh, I.G., (1986)
  !     Whitecaps and the passive remote sensing of the ocean surface,
  !     International Journal of Remote Sensing, 7, pp627-642.
  !
  ! The neutral stability condition is used here (i.e. the difference
  ! between the skin and air temperature is assumed to be zero) so
  ! that the form of the foam coverage equation is the same as in
  ! Tang (1974) and Liu et al. (1998), but with different coefficients.
  ! ===================================================================
  ! Forward model
  SUBROUTINE Foam_Coverage(wind_speed, coverage)
    REAL(fp), INTENT(IN)  :: wind_speed
    REAL(fp), INTENT(OUT) :: coverage
    coverage = FC1 * (wind_speed**FC2)
  END SUBROUTINE Foam_Coverage
  
  ! Tangent-linear model
  SUBROUTINE Foam_Coverage_TL(wind_speed, wind_speed_TL, coverage_TL)
    REAL(fp), INTENT(IN)  :: wind_speed
    REAL(fp), INTENT(IN)  :: wind_speed_TL
    REAL(fp), INTENT(OUT) :: coverage_TL
    coverage_TL = FC3 * (wind_speed**FC4) * wind_speed_TL
  END SUBROUTINE Foam_Coverage_TL

  ! Adjoint model
  SUBROUTINE Foam_Coverage_AD(wind_speed, coverage_AD, wind_speed_AD)
    REAL(fp), INTENT(IN)     :: wind_speed     ! Input
    REAL(fp), INTENT(IN OUT) :: coverage_AD    ! Input
    REAL(fp), INTENT(IN OUT) :: wind_speed_AD  ! Output
    wind_speed_AD = wind_speed_AD + FC3*(wind_speed**FC4)*coverage_AD
    coverage_AD = ZERO
  END SUBROUTINE Foam_Coverage_AD



  ! ============================================
  ! Reflectivity large scale correction routines
  ! ============================================
  !
  SUBROUTINE Large_Scale_Correction( Wind_Speed, cos_z, f, Rv, Rh )
    ! Arguments
    REAL(fp), INTENT(IN)  :: Wind_Speed   ! Wind speed
    REAL(fp), INTENT(IN)  :: cos_z        ! cosine of zenith angle
    REAL(fp), INTENT(IN)  :: f       ! Frequency
    REAL(fp), INTENT(OUT) :: Rv, Rh  ! Reflectivity correction
    ! Local variables
    REAL(fp) :: seczen, zc(12)
    INTEGER :: j
    
    seczen = ONE/cos_z
    ! compute fitting coefficients for a given frequency
    DO j = 1, 12
      zc(j) = Lcoef(j*3-2) + Lcoef(j*3-1)*f + Lcoef(j*3)*f*f
    END DO
       
    Rv = zc(1) + zc(2)*seczen + zc(3)*seczen**2 + zc(4)*Wind_Speed &
       + zc(5)*Wind_Speed**2 + zc(6)*Wind_Speed*seczen

    Rh = zc(7) + zc(8)*seczen + zc(9)*seczen**2 + zc(10)*Wind_Speed &
       + zc(11)*Wind_Speed**2 + zc(12)*Wind_Speed*seczen                                                        

    RETURN
  END SUBROUTINE Large_Scale_Correction
!
!
  SUBROUTINE Large_Scale_Correction_TL( Wind_Speed, cos_z, f, Wind_Speed_tl, Rv_tl, Rh_tl )
    ! Arguments
    REAL(fp), INTENT(IN)  :: Wind_Speed   ! Wind speed
    REAL(fp), INTENT(IN)  :: Wind_Speed_tl   
    REAL(fp), INTENT(IN)  :: cos_z        ! cosine of zenith angle
    REAL(fp), INTENT(IN)  :: f       ! Frequency
    REAL(fp), INTENT(OUT) :: Rv_tl, Rh_tl  ! Reflectivity correction
    ! Local variables
    REAL(fp) :: seczen, zc(12)
    INTEGER :: j
    
    seczen = ONE/cos_z
    ! compute fitting coefficients for a given frequency
    DO j = 1, 12
      zc(j) = Lcoef(j*3-2) + Lcoef(j*3-1)*f + Lcoef(j*3)*f*f
    END DO
       
    Rv_tl = zc(4)*Wind_Speed_tl &
       + TWO*zc(5)*Wind_Speed*Wind_Speed_tl + zc(6)*Wind_Speed_tl*seczen

    Rh_tl = zc(10)*Wind_Speed_tl &
       + TWO*zc(11)*Wind_Speed*Wind_Speed_tl + zc(12)*Wind_Speed_tl*seczen                                                        
  
    RETURN
  END SUBROUTINE Large_Scale_Correction_TL
!
!
  SUBROUTINE Large_Scale_Correction_AD( Wind_Speed, cos_z, f, Rv_ad, Rh_ad, Wind_Speed_ad )
    ! Arguments
    REAL(fp), INTENT(IN)   :: Wind_Speed   ! Wind speed
    REAL(fp), INTENT(INOUT)  :: Wind_Speed_ad  
    REAL(fp), INTENT(IN)   :: cos_z        ! cosine of zenith angle
    REAL(fp), INTENT(IN)   :: f       ! Frequency
    REAL(fp), INTENT(IN)   :: Rv_ad, Rh_ad  ! Reflectivity correction
    ! Local variables
    REAL(fp) :: seczen, zc(12)
    INTEGER :: j
    
    seczen = ONE/cos_z
    ! compute fitting coefficients for a given frequency
    DO j = 1, 12
      zc(j) = Lcoef(j*3-2) + Lcoef(j*3-1)*f + Lcoef(j*3)*f*f
    END DO

    Wind_Speed_ad = Wind_Speed_ad + zc(10)*Rh_ad &
       + TWO*zc(11)*Wind_Speed*Rh_ad + zc(12)*Rh_ad*seczen                                                        
         
    Wind_Speed_ad = Wind_Speed_ad + zc(4)*Rv_ad &
       + TWO*zc(5)*Wind_Speed*Rv_ad + zc(6)*Rv_ad*seczen
    
    RETURN
  END SUBROUTINE Large_Scale_Correction_AD



  ! =============================================================
  ! Routine for forward model foam reflectivity
  ! Function dependence is on zenith angle only
  ! so no TL or AD routine.
  ! See Eqns(18.44a) (18.44b) in
  !   Ulaby, F.T. et al. (1986) Microwave Remote Sensing, Active
  !     and Passive, vol.3, From Theory to Applications, pp1457.
  ! =============================================================
  SUBROUTINE Foam_Reflectivity(z, Frequency, Rv, Rh)
    ! Arguments
    REAL(fp), INTENT(IN)  :: z, Frequency ! Zenith angle
    REAL(fp), INTENT(OUT) :: Rv, Rh
    ! Parameters
    REAL(fp), PARAMETER :: FR_COEFF(9) = &
      (/ -9.946e-4_fp, 3.218e-5_fp, -1.187e-6_fp, &
            7.e-20_fp,     0.07_fp, -1.748e-3_fp, &
         -7.336e-5_fp, 1.044e-7_fp,     -0.93_fp /)
    REAL(fp), PARAMETER :: f0 = 30.0_fp
    ! Local variables
    REAL(fp) :: Fv, Fh, Foam_ref
    ! The vertical component    
    Fv = ONE + z*(FR_COEFF(1)+ z*(FR_COEFF(2) + z*FR_COEFF(3))) + FR_COEFF(4)*z**10
    Rv = FR_COEFF(5)
    ! The horizontal component
    Fh = ONE + z*(FR_COEFF(6) +  z*(FR_COEFF(7) + z*FR_COEFF(8)))
    Rh = ONE + FR_COEFF(9)*Fh
    
    ! Added frequency dependence derived from Stogry model
    Foam_ref = 0.4_fp * exp(-0.05_fp*Frequency )
    Rv = Rv * Foam_ref
    Rh = Rh * Foam_ref
!!    Foam_ref = 2.0_fp * (Frequency + f0)/( TWO*Frequency + f0 )
!!    Rv = Rv * Foam_ref
!!    Rh = Rh * Foam_ref  
    


  END SUBROUTINE Foam_Reflectivity  
  
  ! ============================================
  ! Reflectivity small scale correction routines
  ! ============================================
  !
  SUBROUTINE Small_Scale_Correction( Wind_Speed, f, iVar )
    ! Arguments
    REAL(fp), INTENT(IN)  :: Wind_Speed   ! Wind speed
    REAL(fp), INTENT(IN)  :: f       ! Frequency
    TYPE(iVar_type),    INTENT(INOUT) :: iVar    ! iVar%small_corr is small correction factor.
    
  ! Apply small-scale correction      
  ! --------------------------------
    iVar%windspeed = Wind_Speed  
    IF( iVar%windspeed < min_wind ) iVar%windspeed = min_wind
    IF( iVar%windspeed > max_wind ) iVar%windspeed = max_wind
    iVar%freq_S = f
    IF( iVar%freq_S < min_f ) iVar%freq_S = min_f
    IF( iVar%freq_S > max_f ) iVar%freq_S = max_f  
    
    iVar%scor = Scoef(1) *iVar%windspeed*iVar%freq_S +Scoef(2) *iVar%windspeed*iVar%freq_S**2 &
           + Scoef(3) *iVar%windspeed**2* iVar%freq_S +Scoef(4) *iVar%windspeed**2* iVar%freq_S**2 &
           + Scoef(5) *iVar%windspeed**2 /iVar%freq_S +Scoef(6) *iVar%windspeed**2 /iVar%freq_S**2 &
           + Scoef(7) *iVar%windspeed + Scoef(8) *iVar%windspeed**2
      
    iVar%small_corr = exp(-iVar%scor*iVar%cos_z*iVar%cos_z )

    RETURN
  END SUBROUTINE Small_Scale_Correction
!
  ! ============================================
  ! Reflectivity small scale correction routines TL
  ! ============================================
  !
  SUBROUTINE Small_Scale_Correction_TL( Wind_Speed_TL, iVar, small_corr_TL )
    ! Arguments
    TYPE(iVar_type),    INTENT(IN) :: iVar
    REAL(fp),    INTENT(IN) :: Wind_Speed_TL
    REAL(fp),    INTENT(OUT) :: small_corr_TL     ! small_corr_TL is small correction factor TL.
    ! Local variables
    REAL(fp) :: scor_TL, windspeed_TL
    
 ! Small-scale correction TL   
  ! --------------------------------
    windspeed_TL = Wind_Speed_TL
    IF( iVar%windspeed < min_wind ) windspeed_TL = ZERO
    IF( iVar%windspeed > max_wind ) windspeed_TL = ZERO
    scor_TL = ( Scoef(1)*iVar%freq_S +Scoef(2)*iVar%freq_S**2 &
            +TWO*Scoef(3) *iVar%windspeed*iVar%freq_S +TWO*Scoef(4)*iVar%windspeed* iVar%freq_S**2 &
            +TWO*Scoef(5) *iVar%windspeed /iVar%freq_S +TWO*Scoef(6) *iVar%windspeed/iVar%freq_S**2 &
            +Scoef(7) + TWO*Scoef(8) *iVar%windspeed  )*windspeed_TL

    small_corr_TL = -scor_TL*iVar%cos_z*iVar%cos_z* iVar%small_corr

    RETURN
  END SUBROUTINE Small_Scale_Correction_TL
!
!
  ! ============================================
  ! Reflectivity small scale correction routines TL
  ! ============================================
  !
  SUBROUTINE Small_Scale_Correction_AD( small_corr_AD, iVar, Wind_Speed_AD )
    ! Arguments
    TYPE(iVar_type),    INTENT(IN) :: iVar
    REAL(fp),    INTENT(INOUT) :: small_corr_AD
    REAL(fp),    INTENT(INOUT) :: Wind_Speed_AD
    ! Local variables
    REAL(fp) :: scor_AD, windspeed_AD
    
 ! Small-scale correction AD
    scor_AD = -small_corr_AD*iVar%cos_z*iVar%cos_z* iVar%small_corr   
    windspeed_AD = Wind_Speed_AD + ( Scoef(1)*iVar%freq_S +Scoef(2)*iVar%freq_S**2 &
            +TWO*Scoef(3) *iVar%windspeed*iVar%freq_S +TWO*Scoef(4)*iVar%windspeed* iVar%freq_S**2 &
            +TWO*Scoef(5) *iVar%windspeed/iVar%freq_S +TWO*Scoef(6) *iVar%windspeed/iVar%freq_S**2 &
            +Scoef(7) + TWO*Scoef(8) *iVar%windspeed  )*scor_AD
    IF( iVar%windspeed < min_wind ) windspeed_AD = ZERO
    IF( iVar%windspeed > max_wind ) windspeed_AD = ZERO    
    Wind_Speed_AD = windspeed_AD
    small_corr_AD = ZERO
    RETURN
  END SUBROUTINE Small_Scale_Correction_AD
!
!
END MODULE CRTM_Fastem4
!
