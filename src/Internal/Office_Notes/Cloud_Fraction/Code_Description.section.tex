\section{Code Description}
%=========================
\label{sec:code_description}

\subsection{Overlap/cloud cover definitions}
%-------------------------------------------

Given a cloud fraction ($f_k$) profile for an input atmosphere of $k = 1,\ldots,K$ layers, a cloud cover (CC) profile can be generated from the cloud fraction profile differently depending on the user-selected methodology. This cloud cover profile is then used to average the 100\% clear and 100\% cloudy channel radiances by selecting the last layer value, $CC_K$, as the total cloud cover (TCC),
\begin{equation}
  R_{\nu,allsky} = (1 - TCC).R_{\nu,clear} + TCC.R_{\nu,cloudy}
\end{equation}

Three overlap schemes are available for selection: maximum, random, and maximum/random. The formlations used in this implementation were taken from \citet{MorcretteJakob_2000} as shown below.

For the maximum overlap assumption, we have
\begin{equation}
  CC_{k,max} = \max_{i=1,k}f_{i}\qquad\mbox{for each}\quad k = 1,\ldots,K
  \label{eqn:maximum_cc}
\end{equation}
The random overlap assumption gives,
\begin{equation}
  CC_{k,ran} = 1 - \prod_{i=1}^k{(1 - f_{i})}\qquad\mbox{for each}\quad k = 1,\ldots,K
  \label{eqn:random_cc}
\end{equation}
And, finally, for the maximum-random overlap assumption, we use
\begin{equation}
  CC_{k,maxran} = 1 - \prod_{i=1}^k{\frac{1 - \max(f_i,f_{i-1})}{(1 - f_{i-1})}}\qquad\mbox{for each}\quad k = 1,\ldots,K
  \label{eqn:maxran_cc}
\end{equation}

In addition to these typical overlap assumptions, the methodolgogy for computing total cloud cover based on a weighted average of the cloud water amounts in each layer \citep{Geer_2009} is also included, where
\begin{equation}
  CC_{k,ave} = \frac{\displaystyle \sum_{i=1}^k{\left( \textstyle \sum_{n=1}^N{q_{n,i}} \right) f_i}}{\displaystyle \sum_{i=1}^k{\left( \textstyle \sum_{n=1}^N{q_{n,i}} \right)}}\qquad\mbox{for each}\quad k = 1,\ldots,K
  \label{eqn:average_cc}
\end{equation}
where $N$ represents the number of cloud \emph{types} in a layer and $q_{n,k}$ the cloud amount for cloud type $n$ at layer $k$.

\subsection{Specifying the cloud fraction profile}
%-------------------------------------------------

The cloud fraction for an atmospheric profile is supplied via the new \f{Cloud\_Fraction} component in the CRTM \f{Atmosphere} object definition.

Each element of the \f{Cloud\_Fraction} component should be set to the cloud fraction, $f_k$, of that layer, for example

\begin{alltt}
  TYPE(CRTM_Atmosphere_type) :: atm
  ...
    \textrm{\textit{allocate the atmosphere object...}}
  ...
  atm%Cloud_Fraction        = 0.0       ! All layers are cloud free
  atm%Cloud_Fraction(10:14) = 0.314159  ! Layers 10-14 contain clouds\end{alltt}


\subsection{Specifying the overlap/cloud cover methodology}
%----------------------------------------------------------

The default cloud cover computation methodology is the averaging method, equation \ref{eqn:average_cc}. A different cloud cover algorithm can be specified via the \f{Options} input to the CRTM.

The \f{Options} object definition module now includes a \f{CRTM\_Options\_SetValue()} procedure that can be used to select the cloud cover algorithm. For example, if a user wishes to use the maximum-random overlap assumption rather than the default averaging method, the option is specified like so, e.g.

\begin{alltt}
  TYPE(CRTM_Options_type) :: opt(:)
  ....
  ! Set maximum-random overlap for cloud cover
  CALL CRTM_Options_SetValue( opt, Set_MaxRan_Overlap = .TRUE. )\end{alltt}

See section \ref{sec:CRTM_Options_SetValue_interface} for the complete procedure interface.


\subsection{Fractional cloud coverage}
%-------------------------------------

Within the main CRTM procedures (forward, tangent-linear, adjoint, or K-matrix), the cloud cover computations are performed only if the supplied cloud fraction profile indicates fractional cloudiness. This determination is made by the \f{CRTM\_Atmosphere\_Coverage()} procedure given an  input atmosphere object. The bulk of that procedure is replicated below.

\begin{alltt}
  USE CRTM_Parameters, WATER_CONTENT_THRESHOLD
  ...
  ! Local parameters
  REAL(fp), PARAMETER :: MIN_COVERAGE_THRESHOLD = 1.0e-06_fp
  REAL(fp), PARAMETER :: MAX_COVERAGE_THRESHOLD = ONE - MIN_COVERAGE_THRESHOLD
  ...
  ! Default clear
  coverage_flag = CLEAR
  IF ( atm%n_Clouds == 0 ) RETURN

  ! Check each cloud separately
  Cloud_Loop: DO n = 1, atm%n_Clouds
  
    ! Determine if there are ANY cloudy layers
    cloudy_layer_mask = atm%Cloud(n)%Water_Content > WATER_CONTENT_THRESHOLD
    nc = COUNT(cloudy_layer_mask)
    IF ( nc == 0 ) CYCLE Cloud_Loop

    ! Get the indices of those cloudy layers
    idx(1:nc) = PACK([(k, k=1,atm%Cloud(n)%n_Layers)], cloudy_layer_mask)

    ! Check for ANY fractional coverage
    DO k = 1, nc
      IF ( (atm%Cloud_Fraction(idx(k)) > MIN_COVERAGE_THRESHOLD) .AND. &
           (atm%Cloud_Fraction(idx(k)) < MAX_COVERAGE_THRESHOLD) ) THEN
        coverage_flag = FRACTIONAL
        RETURN
      END IF
    END DO

    ! Check for ALL totally clear or totally cloudy
    IF ( ALL(atm%Cloud_Fraction(idx(1:nc)) < MIN_COVERAGE_THRESHOLD) .OR. &
         ALL(atm%Cloud_Fraction(idx(1:nc)) > MAX_COVERAGE_THRESHOLD) ) coverage_flag = OVERCAST
    
  END DO Cloud_Loop\end{alltt}

Note that there are two threshold checks. First, any particular cloud's water content in a layer must exceed the water content threshold (currently $10^{-6}$kg/m$^2$) to be considered. This threshold is also used in the CRTM procedures to retrieve the cloud optical properties.

The second threshold is for the layer cloud fraction itself to specify a tolerance for what constitutes clear or cloudy coverage. In this case an arbitrary value of $10^{-6}$ was used.

Special mention should be made of the last check for totally clear or totally cloudy, in particular the first portion of the test, \texttt{ALL(atm\%Cloud\_Fraction(idx(1:nc)) < MIN\_COVERAGE\_THRESHOLD)}, that still leads to an overcast designation. This check for effective zero cloud coverage at that point is necessary to maintain similar behaviour for existing code using the CRTM where clouds are specified since 100\% cloudiness is the default for previous versions of the CRTM.


\subsection{The \f{CloudCover} object definition}
%------------------------------------------------

The \f{CloudCover} object and method definitions are shown in detail in appendix \ref{sec:cloudcover_object}. The actual object definition is replicated here to highlight differences with other CRTM-related object definitions.

\begin{alltt}
  TYPE :: CRTM_CloudCover_type
    ! Dimension
    INTEGER :: n_Layers = 0                             ! K dimension.
    ! Data
    INTEGER  :: Overlap           = DEFAULT_OVERLAP_ID  ! Overlap type identifier
    REAL(fp) :: Total_Cloud_Cover = ZERO                ! Cloud cover used in RT
    REAL(fp), ALLOCATABLE :: Cloud_Fraction(:)          ! K. The physical cloud fraction
    REAL(fp), ALLOCATABLE :: Cloud_Cover(:)             ! K. The overlap cloud cover
  CONTAINS
    PRIVATE
    PROCEDURE, PUBLIC, PASS(self)    :: Overlap_Id
    PROCEDURE, PUBLIC, PASS(self)    :: Overlap_Name
    PROCEDURE, PUBLIC, PASS(self)    :: Compute_CloudCover
    PROCEDURE, PUBLIC, PASS(self_TL) :: Compute_CloudCover_TL
    PROCEDURE, PUBLIC, PASS(self_AD) :: Compute_CloudCover_AD
    PROCEDURE, PUBLIC, PASS(self)    :: Is_Usable
    PROCEDURE, PUBLIC, PASS(self)    :: Destroy
    PROCEDURE, PUBLIC, PASS(self)    :: Create
    PROCEDURE, PUBLIC, PASS(self)    :: Inspect
    PROCEDURE, PUBLIC, PASS(self)    :: Set_To_Zero
    PROCEDURE :: Equal
    PROCEDURE :: NotEqual
    PROCEDURE :: Compare_
    GENERIC, PUBLIC :: OPERATOR(==) => Equal
    GENERIC, PUBLIC :: OPERATOR(/=) => NotEqual
    GENERIC, PUBLIC :: OPERATOR(.Compare.) => Compare_
  END TYPE CRTM_CloudCover_type\end{alltt}

The definition makes use of some of the object oriented features of Fortran2003/2008 by specifying procedures as part of the object definition (commonly referred to as type-bound procedures).

This simplifies the usage of the \f{CRTM\_CloudCover\_Define} module since now only the type needs to be referenced, e.g.

\begin{alltt}
  USE CRTM_CloudCover_Define, ONLY: CRTM_CloudCover_type\end{alltt}

while still providing access to all of the procedures defined within.

It also changes the calling interface from a more ``CRTM-like'' call of

\begin{alltt}
  TYPE(CRTM_CloudCover_type) :: cc_obj
  ...
  CALL CRTM_CloudCover_Inspect( cc_obj )\end{alltt}

to

\begin{alltt}
  TYPE(CRTM_CloudCover_type) :: cc_obj
  ...
  CALL cc_obj%Inspect()\end{alltt}

This is mentioned here because the next major version release of the CRTM (v3) is planned to include the community Surface Emissivity Model (CSEM) and the CRTM objects visible in teh main procedure interfaces will be modified in a similar form to allow type-extension of CSEM objects.
